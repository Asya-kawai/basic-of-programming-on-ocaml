@import: settings

% https://dev.realworldocaml.org/first-class-modules.html
% https://v2.ocaml.org/manual/firstclassmodules.html
% 第一級モジュールの説明をどこかに入れる。
% と思ったが、なかなか説明が膨大になるのでやめる。

let modules = '<
  +p{
    % https://cs3110.github.io/textbook/chapters/modules/intro.html
    % https://dev.realworldocaml.org/files-modules-and-programs.html - Files, Modules, and Programs
    ここまでは、utopというトップレベルでコードを書いてきました。
    これはプログラムを単一ファイルに書くようなものです。
    小さなプログラムを書くだけであれば、1つのファイルに全てを詰め込んで良いかも知れません。
    しかし、大きく複雑なプログラムを書く場合、単一ファイルに全てを記述しそれを把握することが困難な場合があります。
    そのようなケースでは、プログラムの関心や責務に応じてコードを分割しますが、この分割した単位を\dfn{モジュール}といいます\footnote{
      % https://dev.realworldocaml.org/first-class-modules.html
      OCamlのモジュールは、変数や関数と同じように値として扱うことができ、これを\dfn{第一級モジュール}と言います。
    }。
    % https://dev.realworldocaml.org/files-modules-and-programs.html - Multifile Programs and Modules
    OCamlでは、1つのファイルは1つのモジュールに対応します。
    つまり、プログラムの関心や責務といった概念単位でファイル分割を行うと、自動的にモジュールに分割したことになります。
    このようにモジュールによって大規模ソフトウェアの複雑さに立ち向かう方法を、\dfn{モジュラープログラミング}といいます。
    本章ではモジュールを利用したプログラミングを説明します。
  }
  +section?:(`Modules`){モジュール}<
    +p{
      % プログラミングの基礎 第19章 モジュール
      % https://v2.ocaml.org/manual/moduleexamples.html
      モジュールとは、プログラムの関心や責務に応じてコードを分割またはまとめたものです。
      モジュールを定義するには以下の構文を利用します。

      \d-code(```
      module <module> = struct
        <body>
      end
      ```);%

      \codem{\<module\>}はモジュール名を表し、\codem{\<body\>}はモジュール本体を表します。
      モジュール本体にはモジュール固有の変数や関数を定義します。

      % https://v2.ocaml.org/manual/moduleexamples.html
      % https://cs3110.github.io/textbook/chapters/modules/encapsulation.html
      関連する変数や関数をまとめることを\dfn{パッケージング}または\dfn{カプセル化}と言い、
      このようなまとまり（モジュール本体）をパッケージまたは\dfn{構造体}と言います。
      構造体は\codem{sturct \<body\> end}で表されます。
      このように構造体にモジュール名を付けることを\dfn{モジュールバインディング}と言います。

      モジュール定義をEBNFで表すと以下のようになります。

      % https://v2.ocaml.org/manual/modules.html#s%3Amodule-expr
      % https://v2.ocaml.org/manual/modules.html#sss:mexpr-module-defs
      \d-code(```
      <module-expr>  ::= ...
                       | struct [ <module-items> ] end
                       | ...

      <module-items> ::= { ;; }
                         ( <definition> | <expr> ) { { ;; }
                           ( <definition> | <expr> ) }
                         { ;; }

      <definition>   ::= module <module-name> {
                           ( <module-name> : <module-type> )
                         } [:module-type] = <module-expr>
                       | let [rec] <let-binding> { and <let-binding> }
                       | ...

      <module-name>  ::= (A..Z) { a..zA..Z | 0..9 | _ | ' }
      ```);%

      上記の構文は次のルールを表しています。
      \listing{
        * モジュール式（\<module-expr\>）はstruct〜endで表現できます
        * モジュール内には、モジュール定義やletバインディングを記述できます
        * モジュール定義（\<definition\>）は、\codem{module \<module-name\> = \<module-expr\>}で表現できます
        * モジュール名（\<module-name\>）は先頭大文字で始める必要があります
      }%

      モジュール定義の例を以下に示します。

      \d-code(```
      utop # module Mymath = struct
        let plus x y = x + y
        let minus x y = x - y
      end ;;
      module Mymath :
        sig val plus : int -> int -> int val minus : int -> int -> int end
      ```);%

      上記はモジュール\codem{Mymath}を定義しています。
      Mymathモジュールは、2つの引数を取ってそれらを可算する関数\codem{plus}と、
      2つの引数を取ってそれらを減算する関数\codem{minus}を持ちます。
      インタプリタの出力は「Mymathというモジュールが定義され、sig〜end（plus、minus関数）の仕様を持つ」という旨を示しています。
      このsig〜endはモジュールの仕様を示しており、これを\dfn{シグネチャ}と言います。
    }
    +p{
      モジュールに定義された変数や関数を使用するには\codem{\<module-name\>.\<variable\>}の形式で呼び出します。
      以下にその例を示します。
      \d-code(```
      utop # Mymath.plus ;;
      - : int -> int -> int = <fun>

      utop # Mymath.minus ;;
      - : int -> int -> int = <fun>
      ```);%
    }
  >
  +section?:(`Signatures`){シグネチャ}<
    +p{
      % プログラミングの基礎 第19章 モジュールインターフェース：シグネチャ
      % 抽象データ型をここで説明する
      % https://dev.realworldocaml.org/files-modules-and-programs.html - Signatures and Abstract Types
      シグネチャとは、モジュールがどのような変数や関数等を持つか、といったモジュールのインターフェースを示します\footnote{
        % https://dev.realworldocaml.org/files-modules-and-programs.html - Signatures and Abstract Types
        OCamlでは、インターフェース、シグネチャ、モジュールタイプは全て同じ意味で扱われます。
      }。
      モジュールを定義するとシグネチャも自動で生成されます。
      しかし、モジュール内でしか利用しない変数や関数といった、モジュールの利用者に公開しなくても良い情報を含む場合があります。
      このようにモジュールが提供するべき変数や関数等だけを公開し、それ以外を隠蔽するのがシグネチャです。
      % https://cs3110.github.io/textbook/chapters/modules/encapsulation.html
      このように外部に公開する必要のない情報を隠すこと及び
      公開しない情報へアクセスさせないことを\dfn{情報隠蔽}と言います\footnote{
        % https://ja.wikipedia.org/wiki/%E3%82%AB%E3%83%97%E3%82%BB%E3%83%AB%E5%8C%96
        特定のデータや操作をまとめ詳細を隠蔽することをカプセル化と定義する場合もあるため、
        カプセル化と情報隠蔽は同義と見なされることもあります。
      }。

      シグネチャを定義するには以下の構文を利用します。

      \d-code(```
      module type <signature> = sig
        <body>
      end
      ```);%

      \codem{\<signature\>}はシグネチャ名を表し、\codem{\<body\>}はシグネチャ本体を表します。
      シグネチャ本体にはモジュールで利用する型、変数の型、関数の型等を定義します。
      EBNFで表すと以下のようになります\footnote{
        分かりやすさのため簡略化したEBNFを記載しています。
      }。

      % https://v2.ocaml.org/manual/modtypes.html#hevea_manual.kwd150
      % シグネチャ名の参考に。https://v2.ocaml.org/manual/names.html#modtype-path
      \d-code(```
      <module-type>   ::= ...
                        | sig { <specification> [;;] } end
                        | ...

      <specification> ::= val <value-name> : <typexpr>
                        | module <module-name> : <module-type>
                        | exception <constr-decl>
                        | ...

      <module-type>   ::= ...
                        | (a..zA..Z) { a..zA..Z | 0..9 | _ | ' }
                        | ...
      ```);%

      上記の構文は次のルールを表しています。
      \listing{
        * モジュールの型（\<module-type\>）はsig〜endで表現できます
        * sig〜end内には、0個以上の定義（\<specification\>）を定義できます
        * 定義は、変数、関数、例外等を含みます
        * シグネチャ名は、先頭が大文字または小文字で始める必要があります
      }%

      シグネチャ定義の例を以下に示します。
      \d-code(```
      utop # module type Mymath_t = sig
        type t
        val of_int : int -> t
        val plus : t -> t -> t
        val minus : t -> t -> t
        val to_int : t -> int
      end ;;
      module type Mymath_t =
        sig
          type t
          val of_int : int -> t
          val plus : t -> t -> t
          val minus : t -> t -> t
          val to_int : t -> int
        end
      ```);%

      上記は\codem{Mymath_t}というシグネチャを定義しています。
      Mymath_tシグネチャは、tという型を持ちます。
      \ref-section(`Modules`);で定義したMymathモジュールはint型を受け取りましたが、
      シグネチャでMymath専用の型tを定義しこれをMymathモジュールの中で利用することで、
      プログラマに対して型tがint型であることを隠蔽します。
      別の文脈で定義または利用したint型の値を、意図せずMymathモジュールの関数に適用することを防げるというメリットがあります\footnote{
        % プログラミングの基礎 第19章 19.4 抽象データ型
        % https://ja.wikipedia.org/wiki/%E6%8A%BD%E8%B1%A1%E3%83%87%E3%83%BC%E3%82%BF%E5%9E%8B
        このようにモジュールで利用可能な型が具体的に何であるか（ここではint型）を隠蔽し、それを利用する関数によってのみ規定されるデータ型を\dfn{抽象データ型}と言います。ここではMymath.tが抽象データ型になります。

        % https://dev.realworldocaml.org/files-modules-and-programs.html - Signatures and Abstract Types
        % https://dev.realworldocaml.org/files-modules-and-programs.html - Concrete Types in Signatures
        % 上記は初学者には難しいと感じたので割愛。
        % 読んだ感じ、注腸データ型の具体例を述べているだけ？
      }。

      Mymath_tシグネチャは型tを利用するいくつかの関数も提供します。
      int型の値を受け取ってt型に変換する関数\codem{of_int}、その逆でt型の値を取ってint型に変換する関数\codem{to_int}、
      t型の値を2つ取ってその可算結果を返す関数plus、t型の値を2つ取ってその減算結果を返す関数minusを提供します。
    }
    +p{
      % プログラミングの基礎 第19章 19.5 そのほかのシグネチャの宣言法
      これをモジュールにアタッチするには次の構文を利用します。

      \d-code(```
      module <module> : <signature> = struct
        <body>
      end
      ```);%

      例えば、Mymath_tシグネチャを持つMymathモジュールを定義するには以下のように記述します。

      \d-code(```
      utop # module Mymath : Mymath_t = struct
        type t = int
        let of_int x = x
        let plus x y = x + y
        let minus x y = x - y
        let to_int x = x
      end ;;
      module Mymath : Mymath_t
      ```);%

      これを用いて可算や減算を行うには以下のように実行します。

      \d-code(```
      utop # Mymath.of_int 1 ;;
      - : Mymath.t = <abstr>

      utop # Mymath.to_int (Mymath.of_int 2) ;;
      - : int = 2

      utop # let result =
        let three = Mymath.of_int 3 in
        let four = Mymath.of_int 4 in
        Mymath.plus three four ;;
      val result : Mymath.t = <abstr>

      utop # Mymath.to_int result ;;
      - : int = 7

      utop # let result =
        let three = Mymath.of_int 3 in
        let four = Mymath.of_int 4 in
        Mymath.minus three four ;;
      val result : Mymath.t = <abstr>

      utop # Mymath.to_int result ;;
      - : int = -1
      ```);%
    }
    +p{
      % プログラミングの基礎 第19章 19.5 そのほかのシグネチャの宣言法
      また、モジュールとシグネチャの定義をまとめて行うことも可能です。
      その場合は、以下の構文を利用します。

      \d-code(```
      module <module> : sig
        <signature-body>
      end = struct
        <module-body>
      end
      ```);%

      \<signature-body\>にシグネチャの定義を書き、\<module-body\>に実際の定義を書きます。
      Mymathを上記の構文で定義した例を以下に示します。

      \d-code(```
      utop # module Mymath : sig
        type t
        val of_int : int -> t
        val plus : t -> t -> t
        val minus : t -> t -> t
        val to_int : t -> int
      end = struct
        type t = int
        let of_int x = x
        let plus x y = x + y
        let minus x y = x - y
        let to_int x = x
      end ;;
      module Mymath :
        sig
          type t
          val of_int : int -> t
          val plus : t -> t -> t
          val minus : t -> t -> t
          val to_int : t -> int
        end
      ```);%
    }
  >
  +section?:(`Nested modules`){ネストしたモジュール}<
    +p{
      % https://dev.realworldocaml.org/files-modules-and-programs.html - Nested Modules
      \ref-section(`Modules`);のEBNFから分かるように、モジュール内にモジュールを定義することもできます。
      つまりモジュールはネストが可能です。

      ネストしたモジュールの例を以下に示します。
      % https://cs3110.github.io/textbook/chapters/modules/modules.html
      \d-code(```
      utop # module A = struct
        let a = "a"
        module B = struct
          let one = 1
        end
      end ;;
      module A : sig val a : string module B : sig val one : int end end

      utop # A.a ;;
      - : string = "a"

      utop # A.B.one ;;
      - : int = 1
      ```);%

      上記の例は、モジュールA内にモジュールBを定義しています。
      モジュールB内の変数oneを利用するには、A.B.oneのようにドット（.）でつなぎます。

      % https://v2.ocaml.org/manual/moduleexamples.html#s:module:structures
      これを\dfn{ドット表記}と言います。
      モジュールのドット表記をEBNFで表すと以下のようになります。

      % https://v2.ocaml.org/manual/modules.html#module-expr
      % https://v2.ocaml.org/manual/names.html#module-path
      \d-code(```
      <module-expr>  ::= <module-path>
                       | struct [ <module-items> ] end
                       | ..

      <module-path>  ::= <module-name> { .<module-name> }

      <module-name>  ::= (A..Z) { a..zA..Z | 0..9 | _ | ' }
      ```);%

      上記の構文は次のルールを表しています。
      \listing{
        * モジュール式（\<module-expr\>）は\<module-path\>またはstruct〜endで表現できます
        * モジュールパス（\<module-path\>）は、モジュール名（\<module-name\>）をドット（.）でつないで表現できます
        * モジュール名（\<module-name\>）は先頭大文字で始める必要があります
      }%
    }
    +p{
      % https://dev.realworldocaml.org/files-modules-and-programs.html - Nested Modules
      より実用的な例としてID管理をあげます。
      システムでユーザデータやアイテムデータ等を扱う場合、IDを持つことがあります。
      これらのIDをプログラムで構造的に管理するために、IDに関する操作をまとめたシグネチャ（IDシグネチャ）を定義したいと仮定します。
      このIDシグネチャをユーザとアイテムに関する操作を定義したモジュール（ユーザモジュール、アイテムモジュール）に適用し、ユーザモジュールとアイテムモジュールがIDを管理できるようにする例を以下に示します。

      \d-code(```
      utop # module type ID_t = sig
        type t
        val of_string : string -> t
        val to_string : t -> string
      end ;;
      module type ID_t =
        sig type t val of_string : string -> t val to_string : t -> string end

      utop # module User = struct
        module Id : ID_t = struct
          type t = string
          let of_string x = x
          let to_string x = x
        end
      end ;;
      module User : sig module Id : ID_t end

      utop # module Item = struct
        module Id : ID_t = struct
          type t = string
          let of_string x = x
          let to_string x = x
        end
      end ;;
      module Item : sig module Id : ID_t end
      ```);%

      IDを管理するためのID_tシグネチャは、モジュールの型tを持ち、
      文字列を受け取りt型として返す関数of_stringの型と、t型を受け取り文字列に変換して返す関数to_stringの型が定義されています。
      次に、Userモジュール及びItemモジュールにネストされたIdモジュールに対してID_tシグネチャを適用しています。
      ネストされたIdモジュールは、ID_tシグネチャによってt型の実際の型（ここではstring型）が隠蔽されるため、
      User.Id.tとItem.Id.tがたとえ同じ文字列を持つIDであっても、型が異なるため一致することはありません。

      \d-code(```
      utop # let user_id = User.Id.of_string "abc" ;;
      val user_id : User.Id.t = <abstr>

      utop # let item_id = Item.Id.of_string "abc" ;;
      val item_id : Item.Id.t = <abstr>

      utop # user_id = item_id ;;
      Error: This expression has type Item.Id.t
             but an expression was expected of type User.Id.t
      ```);%

      % https://dev.realworldocaml.org/files-modules-and-programs.html - Nested Modules
      このように内部的には同じ文字列で表現されるIDであっても、モジュールによって異なるIDと識別されるため、
      異なる種類のIDを混同してしまうようなミスを事前に防げます。
    }
    +subsection?:(`Opening modules`){モジュールの展開}<
      +p{
        % プログラミングの基礎 第20章 20.6 Open文
        % https://dev.realworldocaml.org/files-modules-and-programs.html = Opening Modules
        モジュールがネストしている場合、毎回モジュール名を書くのは手間です。
        その場合、\codem{open}キーワードを用いてモジュール名を省略できます\footnote{
          % https://cs3110.github.io/textbook/chapters/modules/modules.html
          具体的には、openキーワードによってモジュール内に定義されたものを現在のスコープに展開します。
        }。
        openを利用する構文は以下のとおりです。
        % プログラミングの基礎 第20章 20.6 Open文
        \d-code(```
        open <module>
        ```);%

        \<module\>は展開したいモジュール名を表します。
        これを用いた例を以下に示します。

        \d-code(```
        utop # module A = struct
          module B = struct
            let c = "c"
          end
        end ;;
        module A : sig module B : sig val c : string end end

        utop # A.B.c ;;
        - : string = "c"

        utop # open A.B ;;
        utop # c ;;
        - : string = "c"

        utop # open A ;;
        utop # B.c ;;
        - : string = "c"

        utop # open B ;;
        utop # c ;;
        - : string = "c"
        ```);%

        モジュールAはモジュールBを内包し、モジュールBにはcという変数が定義されていることを示しています。
        モジュール名を省略しない場合、A.B.cのように呼び出す必要がありました。
        \codem{open A.B}のように指定することでモジュール内に定義された変数（ここではc）等をそのまま記述できます。
        また\codem{open A}、\codem{open B}のように、モジュール名を個別に指定することも可能です\footnote{
          だだし、open Bはopen Aの後でしか呼び出せません。
        }。        
      }
      +p{
        % https://dev.realworldocaml.org/files-modules-and-programs.html - Open Modules Rarely
        だだし、openするスコープは最小限に留めるのがベストプラクティスとされています。
        % https://dev.realworldocaml.org/files-modules-and-programs.html - Prefer Local Opens
        その方法として\dfn{ローカルオープン}があり、
        これはオープンの範囲（モジュール名の省略できる範囲）を任意の式に制限するものです。
        ローカルオープンはEBNFで以下のように表されます。
        \d-code(```
        <expr>        ::= ...
                        | <local-open>

        <local-open>  ::= let open <module-path> in <expr>

        <module-path> ::= <module-name> { .<module-name> }
        ```);%

        上記の構文は次のルールを表しています。
        \listing{
          * ローカルオープンは式です
          * ローカルオープンはletキーワードと併用します
          * オープン対象のモジュールは、1つ以上のモジュール名（複数の場合はドット（.）でつなげる）を取ります
        }%

        % https://dev.realworldocaml.org/files-modules-and-programs.html - Prefer Local Opens
        % https://v2.ocaml.org/manual/expr.html#sss:local-opens
        % https://cs3110.github.io/textbook/chapters/modules/modules.html - Limiting the Scope of Open.
        以下にローカルオープンを利用した例を示します。
        \d-code(```
        utop # module Mymath : sig
          type t
          val of_int : int -> t
          val plus : t -> t -> t
          val minus : t -> t -> t
          val to_int : t -> int
        end = struct
          type t = int
          let of_int x = x
          let to_int x = x
          let plus x y = x + y
          let minus x y = x - y
        end ;;
        module Mymath :
          sig
            type t
            val of_int : int -> t
            val plus : t -> t -> t
            val minus : t -> t -> t
            val to_int : t -> int
          end

        utop # let x = 
          let open Mymath in
          plus (of_int 2) (of_int 3) ;;
        val x : Mymath.t = <abstr>

        utop # Mymath.to_int x ;;
        - : int = 5
        ```);%

        上記は、変数xの式内のみにMymathモジュールを展開しています。
        式内でのみMymathを省略して関数plusや関数of_intを呼び出していることが分かります。
        そのため、以下のように変数xの外ではMymathを省略して呼び出せません。

        \d-code(```
        utop # to_int x ;;
        Error: Unbound value to_int
        ```);%
      }
    >
    % https://dev.realworldocaml.org/files-modules-and-programs.html - Using Module Shortcuts Instead
    % Using Module Shortcuts insteadは不要かな
  >
  +section?:(`Including modules`){モジュールの拡張}<
    +p{
      % https://dev.realworldocaml.org/files-modules-and-programs.html - Including Modules
      既存のシグネチャ及びモジュールに変数や関数等を新たに追加することも可能です。
      これは\codem{include}キーワードを用いて行います。
      includeキーワードを用いてシグネチャを拡張する構文をEBNFで表すと以下のようになります\footnote{
        includeキーワードの用途のみを示しています。
        完全なEBNFではありません。
      }。

      % https://v2.ocaml.org/manual/modtypes.html#module-type
      \d-code(```
      <module-type>   ::= ...
                        | sig { <specification> [;;] } end
                        | ...

      <specification> ::= ...       
                        | include <module-type>
                        | ...

      <module-type>   ::= ...
                        | (a..zA..Z) { a..zA..Z | 0..9 | _ | ' }
                        | ...
      ```);%

      上記から分かるとおり、includeキーワードはsig〜end内に記述できます。
      includeを用いてシグネチャを拡張する例を以下に示します。

      \d-code(```
      utop # module type ID_t = sig
        type t
        val of_string : string -> t
        val to_string : t -> string
      end ;;
      module type ID_t =
        sig type t val of_string : string -> t val to_string : t -> string end

      utop # module type IDv2_t = sig
        include ID_t
        val extend_value : string
      end ;;
      module type IDv2_t =
        sig
          type t
          val of_string : string -> t
          val to_string : t -> string
          val extend_value : string
        end
      ```);%

      上記は、シグネチャID_tを拡張するシグネチャ\codem{IDv2_t}を定義しています。
      ID_tに定義された関数がIDv2_tにも含まれていることが分かります。
    }
    +p{
      また、includeキーワードを用いてモジュールを拡張する構文をEBNFで表すと以下のようになります\footnote{
        シグネチャ同様にincludeキーワードの用途のみを示しています。
      }。

      % https://v2.ocaml.org/manual/modules.html#module-expr
      \d-code(```
      <module-expr>  ::= <module-path>
                       | struct [ <module-items> ] end
                       | <module-expr> ( <module-expr> )
                       | ( <module-expr> : <module-type> )

      <module-items> ::= { ;; }
                         ( <definition> | <expr> ) { { ;; }
                           ( <definition> | <expr> ) }
                         { ;; }

      <definition>   ::= ...
                       | include <module-expr>
                       | ...

      <module-path> ::= <module-name> { .<module-name> } 

      <module-name>  ::= (A..Z) { a..zA..Z | 0..9 | _ | ' }
      ```);%

      上記から分かるとおり、includeキーワードはstruct〜end内に記述できます。
      includeを用いてモジュールを拡張する例を以下に示します。

      \d-code(```
      utop # module Id : ID_t = struct
        type t = string
        let of_string x = x
        let to_string x = x
      end ;;
      module Id : ID_t

      utop # module A = struct
        include Id
        let extend_value = "A"
      end ;;
      module A :
        sig
          type t = Id.t
          val of_string : string -> t
          val to_string : t -> string
          val extend_value : string
        end

      utop # A.of_string ;;
      - : string -> A.t = <fun>
      utop # A.to_string ;;
      - : A.t -> string = <fun>
      utop # A.extend_value ;;
      - : string = "A"
      ```);%

      上記は、includeによってIDモジュールの関数群を持つAモジュールを作成しています。
      また、Aモジュールでは新たにextend_valueという変数も持ちます。
      このようにincludeを用いて既存のモジュールを拡張するモジュールを作成できます。
    }
  >
  +section?:(`Functors`){ファンクタ}<
    +p{
      % https://cs3110.github.io/textbook/chapters/modules/functors.html
      % https://v2.ocaml.org/manual/moduleexamples.html#s:functors
      \ref-section(`Including modules`);ではincludeキーワードによってモジュールの拡張を行いました。
      しかしincludeの場合は、拡張先のモジュールで変数や関数を定義（パラメータ化）する必要があります。
      \dfn{ファンクタ}はパラメータ化を行うための機能であり、よく「モジュールからモジュールへの"関数"」と表現されます。
      なお、ここで"関数"と表現しているのは、概念としては関数ですが
      \ref-section(`Function`);で説明した関数とは異なり、値から値への関数と同様の記述ができないためです。

      ファンクタを定義する構文は以下のとおりです。
      % https://cs3110.github.io/textbook/chapters/modules/functors.html#functor-syntax-and-semantics
      \d-code(```
      module <functor> (<module> : <signature>) ... = ...
      ```);%

      \codem{\<functor\>}はファンクタ名を表し、
      \codem{\<module\> : \<signature\>}はファンクタが受け取るモジュールとその型を表します。
      ファンクタが受け取るモジュールはシグネチャと合わせて、必ずカッコで囲む必要があります\footnote{
        引数にとるモジュールのシグネチャを省略できない点が\ref-section(`Function`);で説明した関数と異なる点です。
      }。
      (\<module\> : \<sigunature\>)の後ろにある3つのドット（...）は「0個以上の引数を取って良い」ことを表します。

      % https://cs3110.github.io/textbook/chapters/modules/functors.html
      % https://dev.realworldocaml.org/functors.html - Functors
      % ファンクタのメリットを最後に書く
      % 結局、書かないかも
      ファンクタの例を以下に示します。
      \d-code(```
      utop # module type X = sig
        val x : int
      end ;;
      module type X = sig val x : int end

      utop # module IncreaseX (M : X) : X = struct
        let x = M.x + 1
      end ;;
      module IncreaseX : functor (M : X) -> X

      utop # module A = struct
        let x = 0
      end ;;
      module A : sig val x : int end

      utop # module B = IncreaseX (A) ;;
      module B : sig val x : int end

      utop # A.x ;;
      - : int = 0

      utop # B.x ;;
      - : int = 1
      ```);%

      上記は、\codem{IncreaseX}というファンクタを定義しています。
      IncreaseXはX型のモジュールMを受け取り、M.x + 1という値に束縛された変数xを持つX型のモジュールを返します。
      % https://dev.realworldocaml.org/functors.html - A Trivial Example
      通常の関数と異なり、ファンクタを定義する場合は必ず引数の型を明示する必要があります。
      なお戻り値の型は必須ではありませんが、戻り値の型を省略した場合は戻り値がX型のモジュールではなく、
      モジュール定義から推論されたシグネチャがそのまま出力されます。
      戻り値を省略した場合の出力例を以下に示します。

      \d-code(```
      utop # module IncreaseX (M : X) = struct
        let x = M.x + 1
      end ;;
      module IncreaseX : functor (M : X) -> sig val x : int end
      ```);%

      % https://cs3110.github.io/textbook/chapters/modules/functors.html
      %また、IncreaseXはパラメータ化された構造体と考えるともでき、通常の関数と同じように引数によってモジュール内の変数や関数等の値を変えることができます。
      また、上記の\codem{module B = IncreaseX (A)}のとおり、IncreaseXは通常の関数と同じように適用できます。
      例から分かるとおりA.xが0であることから、B.xは\codem{let x = M.x + 1 = A.x + 1 = 0 + 1 = 1}となります。
    }
    +p{
      % https://dev.realworldocaml.org/functors.html
      IncreaseXファンクタは、IncreaseXが引数に取る型（ここではXシグネチャ）を満たす任意のモジュールに適用できます。
      この時、IncreaseXが引数に取る型と一致しないものは無視されます。
      その例を以下に示します。

      \d-code(```
      utop # module C = struct
        let x = 2
        let y = "two"
      end ;;
      module C : sig val x : int val y : string end

      utop # module D = IncreaseX(C) ;;
      module D : sig val x : int end

      utop # D.x ;;
      - : int = 3

      utop # D.y ;;
      Error: Unbound value D.y
      ```);%

      上記の例は、Xシグネチャに含まれない変数yを持つCモジュールを定義しています。
      これをIncreaseXに適用し、Dモジュールを定義します。
      Dモジュールは変数xを持ち、この変数の値は3（C.x + 1 = 2 + 1）です。
      しかしファンクタが返すXシグネチャには変数yが含まれないため無視され、結果としてDモジュールに変数yは定義されません。
    }
    +p{
      % https://cs3110.github.io/textbook/chapters/modules/functors.html
      ファンクタは引数に取ったモジュールと異なるモジュールを返すことも可能です。
      以下に引数に取るモジュールと返すモジュールが異なるファンクタの例を示します。
      \d-code(```
      utop # module AddX (M : X) = struct
        let add y = M.x + y
      end ;;
      module AddX : functor (M : X) -> sig val add : int -> int end

      utop # module E = AddX (struct let x = 10 end) ;;
      module E : sig val add : int -> int end

      utop # E.add 3 ;;
      - : int = 13

      utop # E.x ;;
      Error: Unbound value E.x
      ```);%

      上記の例は、AddXというファンクタを定義しています。
      AddXファンクタはX型のモジュールMを受け取り、int型の引数を1つ受け取ってM.x + yを返す関数addを持つシグネチャ（を満たすモジュール）を返します。
      AddXに匿名モジュール\codem{struct let x = 10 end}を適用したものをEモジュールとして定義します。
      上記の例から分かるとおり、Eモジュールはファンクタによって関数addを持ちます。
      なお、ファンクタが返すシグネチャは変数xを持たないため、Eモジュールに変数xは定義されません。
    }
    +p{
      % https://cs3110.github.io/textbook/chapters/modules/functors.html#functor-syntax-and-semantics
      ファンクタを定義する構文から分かるとおり、ファンクタは複数の引数を取ることができます。
      複数の引数を取るファンクタの例を以下に示します。

      \d-code(```
      utop # module type Y = sig
        val y : int
      end ;;
      module type Y = sig val y : a end

      utop # module AddY (M1 : X) (M2 : Y) = struct
        let x = M1.x + M2.y
      end ;;
      module AddY : functor (M1 : X) (M2 : Y) -> sig val x : a end

      utop # module F = AddY (struct let x = 2 end) (struct let y = 3 end) ;;
      module F : sig val x : a end

      utop # F.x ;;
      - : a = 5
      ```);%

      上記は、\codem{AddY}というファンクタを定義しています。
      AddYは、2つの引数（Xシグネチャを持つM1モジュールとYシグネチャを持つM2モジュール）を受け取り、
      M1.x + M2.y という値に束縛された変数xを持つモジュールを返します。
    }
    +p{
      % https://cs3110.github.io/textbook/chapters/modules/functors.html#functor-syntax-and-semantics
      % 匿名ファンクタを説明する
      また、ファンクタは高階関数と同じように、
      1つのファンクタを引数に取り\dfn{匿名ファンクタ}を返す\dfn{高階ファンクタ}で記述できます。
      匿名ファンクタを作るには、\codem{functor}キーワードを利用します。
      つまり以下の2つの構文は同等です。

      % https://cs3110.github.io/textbook/chapters/modules/functors.html#functor-syntax-and-semantics
      % https://v2.ocaml.org/manual/modules.html#sss:mexpr-module-defs
      \d-code(```
      module <functor> (<module_1> : <signature_1>)
                       ...
                       (<module_n> : <signature_n>) = ...

      module <functor> = functor (<module_1> : <signature_1>) ->
                         functor (<module_n> : <signature_n>) -> ...
      ```);%

      AddYファンクタをfunctorキーワードで定義し直すと以下のように書くことができます。
      \d-code(```
      utop # module AddY = functor (M1 : X) -> functor (M2 : Y) -> struct
        let x = M1.x + M2.y
      end ;;
      module AddY : functor (M1 : X) (M2 : Y) -> sig val x : int end
      ```);%
    }
  >
  % 今は脚注で入れるようにする。後から章立てする。
  % +section?:(`First class modules`){第一級モジュール}<
  %   +p{}
  % >
  +section?:(`Using dune`){Duneの利用}<
    +p{
      % http://ocamlverse.net/content/quickstart_ocaml_project_dune.html
      OCamlで大規模なプログラムを管理するには\codem{dune}というビルドシステムを利用すると便利です。
      OCamlのパッケージマネージャである\codem{opam}を用いてduneをインストールできます。
      以下に、ubuntuでのopam及びduneのインストール例を示します\footnote{
        先頭の\codem{\%}はプロンプトを表しています。
      }。

      \d-code(```
      % sudo apt install -y opam

      % opam init
      % opam install dune
      ```);%

      % https://dune.readthedocs.io/en/stable/overview.html#introduction
      duneは\dfn{プロジェクト}という単位で複数のプログラムを管理します。
      duneでプロジェクトを作成するには以下のコマンドを実行します。

      % http://ocamlverse.net/content/quickstart_ocaml_project_dune.html
      \d-code(```
      % dune init proj <project-name>
      ```);%

      例えば、Webアプリケーションのバックエンドとして動作する、HTTPサーバのプロジェクトを作成するとします。
      \codem{my_web_server}というプロジェクト名を作成したい場合、以下のように入力します。

      \d-code(```
      % my-http-serverdune init proj my_web_app
      Success: initialized project component named my_web_app

      % cd my_web_app/
      % ls
      bin/  dune-project  lib/  my_web_app.opam  test/
      ```);%

      duneによって生成されたディレクトリやファイルは次のような意味を持ちます。
      \listing{
        * bin/：実行ファイルを格納するためのディレクトリ
        * bin/dune：実行ファイルをビルドするための設定ファイル
        * bin/mail.ml：実行ファイルの元となるソースコード
        * lib/：プロジェクトのライブラリファイルを格納するためのディレクトリ
        * lib/dune：\dfn{ライブラリ}をビルドするための設定ファイル\footnote{ライブラリとは機能単位にまとめたものを指します。例えば、数学で扱う機能をまとめたライブラリはMathライブラリとして提供されます。}
        * test/：テストコードを格納するためのディレクトリ
        * test/\<proejct_name\>.ml：テストファイル
        * test/dune：テストをビルドするための設定ファイル
        * dune-project：プロジェクトに関する設定を記述するための設定ファイル
        * \<project_name\>.opam：opamで外部パッケージとして提供するための設定ファイル
      }%

      プロジェクトをビルドするには以下のコマンドを実行します。
      下記コマンドはプロジェクト作成直後に実行しても問題ありません。

      \d-code(```
      % dune build
      ```);%

      また、プロジェクトを作成するとダミーのソースコードである\codem{./bin/main.ml}が作成されます。
      main.mlは"Hello, World!"と表示するだけのプログラムが記述されます。
      下記コマンドを実行して、./bin/main.mlを実行できます。

      \d-code(```
      % dune exec ./bin/main.exe
      Hello, World!
      ```);%
    }
    % https://dev.realworldocaml.org/files-modules-and-programs.html WHERE IS main?
    +frame<
      +subsection{コラム；プログラムのスタート地点はどこか}<
        +p{
          いくつかのプログラミング言語であれば、プログラムのスタート地点である関数等が存在します\footnote{
            これをエントリーポイントとも言います。
            いくつかのプログラミング言語では、main関数がその役割を担います。
          }。
          しかしOCamlでは他の言語と異なり、固有のmain関数が存在しません。
          OCamlはソースコード内の変数、関数、式を定義された順（上から下）で評価します。
          そのため最も最後に評価された式の結果がプログラムの結果となります。
          OCamlではイディオムとして、\codem{let () = ...}というUnit型の値へのパターンマッチで記述し、スタート地点とする場合があります。
          これは、プログラムの実行によって戻り値を返すのではなく、副作用によって動作することを示すためです。
        }
      >
    >
    +subsection?:(`Using libraries`){ライブラリの利用}<
      +p{
        % http://ocamlverse.net/content/quickstart_ocaml_project_dune.html Libraries
        duneを利用して作成したプロジェクトでは、libディレクトリで様々なモジュールを管理できます。
        % www2.ocaml.org
        % Libraries, starting with the standard library, provide collections of modules        
        このように1つ以上のモジュールをまとまたものを\dfn{ライブラリ}と言います。

        ここでは四則演算を目的としたMathモジュールを作成する例を示します。
        エディタ等で\codem{lib/math.ml}というファイルを新規作成し、以下の内容を入力し保存してください\footnote{
          分かりやすさのため関数を定義していますが、中置演算子として定義しても良いです。
        }。
  
        \d-code(```
        let plus x y = x + y
        let minus x y = x - y
        ```);%
  
        作成語のlib/及びファイルは以下のようになっています。
        \d-code(```
        % ls lib/
        dune  math.ml
  
        % cat lib/math.ml
        let plus x y = x + y
        let minus x y = x - y
        ```);%
  
        デフォルトでは、ライブラリ名はプロジェクト名（\<project_name\>）が割り当てられます。
        \d-code(```
        % cat lib/dune
        (library
         (name my_web_app))
        ```);%
  
        このライブラリ名を指定する設定をbin/duneに記述することで、bin/以下のファイル群からライブラリを利用できます。
        \d-code(```
        % cat bin/dune
        (executable
         (public_name my_web_app)
         (name main)
         (libraries my_web_app))
        ```);%
  
        % https://cs3110.github.io/textbook/chapters/modules/toplevel.html#dune
        \codem{(libraries my_web_app)}が利用するライブラリを宣言している箇所です。
        lib/duneのライブラリ名とbin/duneの利用するライブラリが一致しているため、
        bin/以下のファイル群から利用できることが分かります。
      }
      +p{
        bin/main.mlを以下の内容に書き換え、lib/math.mlの関数を利用してみます\footnote{
          % https://ocaml.jp/refman/libref/Printf.html
          \codem{Printf.printf}は受け取った引数に従って文字列を整形し、それを標準出力に出すための関数です。
        }。

        \d-code(```
        let () =
          let open My_web_app in
          let result1 = Math.plus 2 3 in
          let result2 = Math.minus 10 5 in
          Printf.printf "Result: %d, %d\n" result1 result2
        ```);%

        lib/duneによって、lib/以下のモジュールは\codem{My_web_app}モジュールに集約されます。
        また、OCamlはファイル単位でモジュールとなるためlib/math.mlは\codem{My_web_app.Math}となります。
        しかし、本プロジェクトではMy_web_appを参照することは明らかであるため、\codem{let open My_web_app}によって、
        当該スコープでMy_web_appを省略できます。
        つまり、本来はMy_web_app.Mathと書く必要があったところをMath.plusやMath.minusで呼び出せるようになります。

        下記コマンドを実行しmain.mlの結果を出力します。
        \d-code(```
        % dune exec ./bin/main.exe
        Result: 5, 5
        ```);%
      }
    >
  >
  +section?:(`Multiple files and Modules`){ファイル分割とモジュール}<
    +p{
      大規模なプログラムを書く場合、複数のモジュールを利用することがあります。
      複数のモジュール定義を1つのファイルにまとめることもできますが、1つのファイルのサイズが大きくなってしまい、
      全体の見通しが悪くなる場合があります。
      その場合、ファイルを分割することで見通しが良くなり、プログラムの可読性が向上し余計なミスを防ぐことができます。
    }
    +p{
      % https://dev.realworldocaml.org/files-modules-and-programs.html - Multifile Programs and Modules
      % https://dev.realworldocaml.org/files-modules-and-programs.html - Signatures and Abstract Types
      % こっちは参照しなかった
      例として\ref-subsection(`Using libraries`);で作成したMathモジュールを拡張することを考えます。

      まずは単一のMathモジュールに機能を追加することを考えます。
      そこで、実数も扱えるようlib/math.mlに以下の定義を追加します\footnote{
        分かりやすさのため関数を定義していますが、中置演算子を定義しても良いです。
      }。
      \d-code(```
      let plus x y = x + y
      let minus x y = x - y
      let plus_f x y = x +. y
      let minus_f x y = x -. y
      ```);%

      bin/main.mlを以下のように書き換え、追加した関数を呼び出します。

      \d-code(```
      let () =
        let open My_web_app in
        let result1 = Math.plus 2 3 in
        let result2 = Math.minus 10 5 in
        let result3 = Math.plus_f 10. 20. in
        let result4 = Math.minus_f 30. 10. in
        Printf.printf "Result: %d, %d, %f, %f\n" result1 result2 result3 result4
      ```);%

      下記コマンドを実行しmain.mlの結果を出力します。

      \d-code(```
      % dune exec ./bin/main.exe
      Result: 5, 5, 30.000000, 20.000000
      ```);%

      次に、Mathモジュールが、
      int型を扱う\codem{Myint}モジュールとfloat型を扱う\codem{Myfloat}モジュールから構成されるように変更します。
      まず、int型を扱うモジュール名をMyintとするため、lib/myint.mlというファイルを新規作成し、
      以下の内容を書き込みます。
      \d-code(```
      let plus x y = x + y
      let minus x y = x - y
      ```);%

      同様にfloat型を扱うモジュール名をMyfloatとするためlib/myfloat.mlというファイルを新規作成し、
      以下の内容を書き込みます。
      \d-code(```
      let plus_f x y = x +. y
      let minus_f x y = x -. y
      ```);%

      最後に、Mathモジュール（lib/math.ml）内でMyintモジュールとMyfloatモジュールをincludeします。
      \d-code(```
      include Myint
      include Myfloat
      ```);%

      myint.ml、myflaot.ml、math.mlは以下のように配置されます。

      \d-code(```
      % ls ./lib/
      dune  math.ml  myfloat.ml  myint.ml
      ```);%

      main.mlを再度実行し、モジュールが呼び出せること及び結果が変わらないことを確認します。
      \d-code(```
      % ls ./bin/main.ml
      ./bin/main.ml

      % cat ./bin/main.ml
      let () =
        let open My_web_app in
        let result1 = Math.plus 2 3 in
        let result2 = Math.minus 10 5 in
        let result3 = Math.plus_f 10. 20. in
        let result4 = Math.minus_f 30. 10. in
        Printf.printf "Result: %d, %d, %f, %f\n" result1 result2 result3 result4

      % dune exec ./bin/main.exe
      Result: 5, 5, 30.000000, 20.000000
      ```);%

      上記の記述によって、bin/main.mlを書き換えることなく、Mathモジュールの見通しを改善できました。
    }
  >
  +section?:(`Using external modules`){外部モジュールの利用}<
    +p{
      Webアプリケーションとして動作させるためには、TCP/IPや適切なセキュリティ等の知識が必要です。
      しかし、プログラマが全てを理解し一からWebアプリケーションを作成することは手間です。
      このように膨大な前提知識を必要とするアプリケーションを作成する場合、
      信頼された第三者が作成した外部ライブラリを利用すると便利です。
      % https://en.wikipedia.org/wiki/Software_framework
      なお、このようにソフトウェアの実装に必要な一般的な機能を持つものを\dfn{フレームワーク}と言います\footnote{
        このフレームワークの定義は諸説あります。
        本書の定義では、ライブラリと同義として扱っても構いません。
      }。
      特にWebアプリケーションに特化したフレームワークをWebアプリケーションフレームワークと言います。
    }
    +p{
      OCamlにはいくつかのWebアプリケーションフレームワークが存在しますが、本書では\dfn{Dream}というフレームワークを利用します。
      opamを用いて以下のようにDreamをインストールできます。
      \d-code(```
      % opam install dream
      ```);%

      次にdreamを利用するため、以下のようにbin/duneの(libraries ...)にdreamを追加します。

      \d-code(```
      (executable
       (public_name my_web_app)
       (name main)
       (libraries dream my_web_app))
      ```);%

      これでbin/以下のファイルからDreamライブラリが利用可能になりました。
      例としてDreamを用いて文字列を返すだけの簡易的なWebサーバを作成します。
      bin/main.mlを以下の内容に書き換え保存してください\footnote{
        外部ライブラリも同様に呼び出せることを示すだけに留めるため、
        Dreamの記述に関する詳細な説明は割愛します。
      }。

      \d-code(```
      let () =
        Dream.run
        @@ Dream.logger
        @@ Dream.router [
        Dream.get "/" (fun _ -> Dream.html ("Good morning, world"))
      ]
      ```);%

      Dreamは\codem{Dream.run}を利用してサーバを起動します。
      サーバはデフォルトで\codem{http://localhost:8080}というURLで起動します。

      これを実行にするため、コマンドラインで以下のように入力して下さい。
      \d-code(```
      % dune exec ./bin/main.exe

      12.11.22 11:05:51.586                       Running at http://localhost:8080
      12.11.22 11:05:51.586                       Type Ctrl+C to stop
      ```);%

      実行するとコマンドは終了せず、\codem{Dream.logger}によってサーバのログが表示されます。
      ログには、サーバがhttp://localhost:8080で起動したこと、サーバを止めるには\codem{Ctrl + C}を入力すれば良いことが示されています。
      試しにブラウザで上記URLにアクセスするとbin/main.ml内の"Good morning, world"が表示されます。

      また、bin/main.mlを以下のように書き換え、自身で作成したlib/math.mlも同様に呼び出すことが可能です。
      \d-code(```
      let () =
        let open My_web_app in
        Dream.run
        @@ Dream.logger
        @@ Dream.router [
        Dream.get "/" (fun _ -> Dream.html (Printf.sprintf "%d" (Math.plus 2 3)))
      ]
      ```);%

      このように外部ライブラリも自身で作成したライブラリと同様に\*/duneの(libraries ...)に記述することで利用できます。
    }
  >
>